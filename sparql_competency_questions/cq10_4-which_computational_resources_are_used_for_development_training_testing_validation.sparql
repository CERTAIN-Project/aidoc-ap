# CQ10.4: Which computational resources are used for development, training, testing and validation?
# Requirement 10: System architecture

PREFIX aidoc: <https://w3id.org/aidoc-ap#>
PREFIX server: <http://w3id.org/devops-infra/server#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?system ?systemLabel ?hardware ?cpu ?memory ?numCPUs ?os ?componentUsingHW ?componentLabel
WHERE {
  ?system a aidoc:AISystem .
  OPTIONAL { ?system rdfs:label ?systemLabel . }
  {
    ?system aidoc:requiresHardware ?hardware .
  } UNION {
    ?system aidoc:hasSoftwareComponent ?componentUsingHW .
    ?componentUsingHW aidoc:requiresHardware ?hardware .
    OPTIONAL { ?componentUsingHW rdfs:label ?componentLabel . }
  }
  OPTIONAL { ?hardware server:cpu ?cpu . }
  OPTIONAL { ?hardware server:memory ?memory . }
  OPTIONAL { ?hardware server:numberCPUs ?numCPUs . }
  OPTIONAL { ?hardware server:operatingSystem ?os . }
}
