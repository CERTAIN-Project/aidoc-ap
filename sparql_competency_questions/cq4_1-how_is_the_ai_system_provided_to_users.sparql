# CQ4.1: How is the AI system provided to users?
# Requirement 4: Deployment forms

PREFIX aidoc: <https://w3id.org/aidoc-ap#>
PREFIX airo: <https://w3id.org/airo#>
PREFIX vair: <https://w3id.org/vair#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?system ?systemLabel ?modality ?modalityType ?deploymentActivity ?deploymentDescription
WHERE {
  ?system a aidoc:AISystem .
  OPTIONAL { ?system rdfs:label ?systemLabel . }
  OPTIONAL { ?system aidoc:modality ?modality . }
  OPTIONAL { ?system airo:hasModality ?modalityType . }
  OPTIONAL {
    ?system aidoc:hasLifecycleStage ?deploymentStage .
    ?deploymentStage a vair:Deployment .
    ?deploymentStage aidoc:hasAIActivity ?deploymentActivity .
    OPTIONAL { ?deploymentActivity dcterms:description ?deploymentDescription . }
  }
}
